# Contributor: Christian Neukirchen <chneukirchen@gmail.com>
# Maintainer: ava1ar <mail(at)ava1ar(dot)me>

pkgname=mosh-git
epoch=1
pkgver=1.4.0.r19.g1105d48
pkgrel=2
pkgdesc="Mobile shell, surviving disconnects with local echo and line editing, development version"
arch=('i686' 'x86_64' 'arm' 'aarch64')
url="https://mosh.org"
license=('GPL-3.0-or-later')
depends=('abseil-cpp' 'gcc-libs' 'glibc' 'libutempter' 'ncurses' 'openssh' 'openssl' 'perl' 'protobuf' 'zlib')
makedepends=('git')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('git+https://github.com/mobile-shell/mosh.git')
b2sums=('SKIP')

pkgver() {
  cd "${pkgname%-git}"
  git describe --long --tags | sed 's/^mosh-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname%-git}"
  ./autogen.sh
  ./configure --enable-ufw --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  DESTDIR="$pkgdir" make -C ${pkgname%-git} install
  install -Dm644 "${pkgname%-git}/conf/bash-completion/completions/${pkgname%-git}" \
    "$pkgdir/usr/share/bash-completion/completions/${pkgname%-git}"
}
